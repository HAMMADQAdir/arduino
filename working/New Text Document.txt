#include <BLEDevice.h>
#include <BLEServer.h>
#include <BLEUtils.h>
#include <BLE2902.h>

#define SERVICE_UUID      "4fafc201-1fb5-459e-8fcc-c5c9c331914b"
#define HR_CHAR_UUID      "885e63c2-8e1a-4f7d-9374-c6307392e604"

BLECharacteristic* hrChar;
bool deviceConnected = false;

class MyServerCallbacks : public BLEServerCallbacks {
  void onConnect(BLEServer* pServer) {
    deviceConnected = true;
    Serial.println("Client Connected");
  }
  void onDisconnect(BLEServer* pServer) {
    deviceConnected = false;
    Serial.println("Client Disconnected");
    pServer->startAdvertising();
  }
};

void setup() {
  Serial.begin(115200);
  delay(500);

  BLEDevice::init("ESP32_NOTIFY_TEST");

  BLEServer *server = BLEDevice::createServer();
  server->setCallbacks(new MyServerCallbacks());

  BLEService *service = server->createService(SERVICE_UUID);

  hrChar = service->createCharacteristic(
    HR_CHAR_UUID,
    BLECharacteristic::PROPERTY_NOTIFY
  );

  BLE2902 *desc = new BLE2902();
  desc->setNotifications(true);   // VERY IMPORTANT
  hrChar->addDescriptor(desc);

  service->start();

  BLEAdvertising *adv = BLEDevice::getAdvertising();
  adv->addServiceUUID(SERVICE_UUID);
  adv->start();

  Serial.println("BLE Started. Waiting for client...");
}

void loop() {
  if (!deviceConnected) return;

  static uint8_t val = 60;

  hrChar->setValue(&val, 1);
  hrChar->notify();

  Serial.print("Sent: ");
  Serial.println(val);

  val++;
  delay(1000);
}
